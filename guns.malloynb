>>>markdown
##### Firearm Background Checks by FBI
This data presents the monthly counts of firearm background checks processed by the FBI, categorized by month, state, and firearm type.
>>>malloy
source: states_lookup_by_state is duckdb.table('./states.csv') extend {
    primary_key: state
}

source: states_lookup_by_code is duckdb.table('./states.csv') extend {
    primary_key: code
}

source: firearm_deaths is duckdb.table('./firearm_deaths.csv') extend {
    rename: death_year is `YEAR`
    measure: total_deaths is sum(cast(DEATHS as number))
    join_one: states_lookup_by_code with STATE
}

source: background_check is duckdb.table('./nics-firearm-background-checks.csv') extend {
    dimension: full_date is concat(`month`, '-01')
    dimension: BackgroundCheckDate is strptime!date(full_date, '%Y-%m-%d')
    dimension: BackgroundCheckYear is BackgroundCheckDate.year
    dimension: BackgroundCheckMonth is strftime!string(BackgroundCheckDate,'%B')

    measure: 
        #Total Background checks
        BackgroundChecks is sum(totals)
        
        #Firearm Type Breakdown:
        HandgunChecks is sum(handgun)
        LongGunChecks is sum(long_gun)
        OtherFirearmChecks is sum(other)
        MultipleFirearmChecks is sum(multiple)
        AdminChecks is sum(admin)
        HandgunvsLongGunRatio is sum(handgun) / sum(long_gun)
        
        #Pre-Pawn Checks:
        PrePawnHandgun is sum(prepawn_handgun)
        PrePawnLongGun is sum(prepawn_long_gun)
        PrePawnOther is sum(prepawn_other)
        PrePawnRate is (sum(prepawn_handgun) + sum(prepawn_long_gun) + sum(prepawn_other)) / sum(totals)
        PrePawnTotal is sum(prepawn_handgun) + sum(prepawn_long_gun) + sum(prepawn_other)

        #Redemption Checks:
        RedemptionHandGun is sum(redemption_handgun)
        RedemptionLongGun is sum(redemption_long_gun)
        RedemptionOther is sum(redemption_other)
        RedemptionRate is (sum(redemption_handgun) + sum(redemption_long_gun) + sum(redemption_other)) / sum(totals)
        TotalRedemtionChecks is (sum(redemption_handgun) + sum(redemption_long_gun) + sum(redemption_other))

        #Returned Firearms:
        ReturnedHandgun is sum(returned_handgun)
        ReturnedLongGun is sum(returned_long_gun)
        ReturnedOther is sum(returned_other)
        ReturnedRate is (sum(returned_handgun) + sum(returned_long_gun) + sum(returned_other)) / sum(totals)
        TotalReturnedChecks is (sum(returned_handgun) + sum(returned_long_gun) + sum(returned_other))

        #Private Sales:
        PrivateSaleHandgun is sum(private_sale_handgun)
        PrivateSaleLongGun is sum(private_sale_long_gun)
        PrivateSaleOther is sum(private_sale_other)
        PrivateSaleRate is (sum(private_sale_handgun) + sum(private_sale_long_gun) + sum(private_sale_other)) / sum(totals)
        TotalPrivateSaleChecks is (sum(private_sale_handgun) + sum(private_sale_long_gun) + sum(private_sale_other))
    
        #Return to Seller:
        ReturntoSellerHandgun is sum(return_to_seller_handgun)
        ReturntoSellerLongGun is sum(return_to_seller_long_gun)
        ReturntoSellerOther is sum(return_to_seller_other)
        ReturntoSellerRate is (sum(return_to_seller_handgun) + sum(return_to_seller_long_gun) + sum(return_to_seller_other)) / sum(totals)
        TotalReturntoSellerChecks is (sum(return_to_seller_handgun) + sum(return_to_seller_long_gun) + sum(return_to_seller_other))
 
        #Permit Measures
        PermitCheckRate is sum(permit) / sum(totals)
        PermitChecks is sum(permit)
        PermitRechecks is sum(permit_recheck)
    
    join_one: states_lookup_by_state with state
} 
>>>markdown
##### Total background checks by year
>>>malloy
# dashboard
run: background_check -> {
    aggregate: BackgroundChecks
    # line_chart
    nest: Check_By_Year is {
        group_by:  BackgroundCheckYear
        aggregate: BackgroundChecks
    }
    # line_chart
    nest: Check_By_Month is {
        group_by:  BackgroundCheckYear
        aggregate: BackgroundChecks
        group_by: BackgroundCheckMonth
    }
}
>>>markdown
##### Total background checks by month
Gun purchases correlate to end of hunting season, Christmas, and tax returns.
>>>malloy
run: background_check -> {
    aggregate: BackgroundChecks
    # bar_chart
    nest: Check_By_Year is {
        group_by:  BackgroundCheckMonth
        aggregate: BackgroundChecks
    }
}
>>>markdown
#### Background checks by state
Firearm background check laws vary by state, but many states require background checks for all firearm sales.
>>>malloy

run: background_check -> {
    group_by: state
    aggregate: BackgroundChecks
}
# dashboard
run: background_check -> {
    group_by: state
    nest: Check_By_Year is {
        group_by: BackgroundCheckYear
        aggregate: BackgroundChecks
    }
    # line_chart
    nest: Check_By_Year_chart is {
        group_by: BackgroundCheckYear
        aggregate: BackgroundChecks
    }
}

# line_chart
run: background_check -> {
    group_by: BackgroundCheckYear
    aggregate: BackgroundChecks
    group_by: state
    limit: 50000
}

run: background_check -> {
    aggregate: BackgroundChecks

    # shape_map
        nest: Heatmap_By_State is {
        group_by:  state
        aggregate: BackgroundChecks
    }
}
>>>markdown
#### Redemption Background Checks
A firearm redemption check is a background check performed using the National Instant Criminal Background Check System (NICS) when a pawned firearm is redeemed. This check verifies that the person redeeming the firearm is not prohibited from owning a gun.
>>>malloy

run: background_check -> {
    aggregate: TotalRedemtionChecks, RedemptionRate

    nest: types_of_guns is {
        aggregate: RedemptionHandGun, RedemptionLongGun,RedemptionOther
    }
}

run: background_check -> {
    group_by:  BackgroundCheckYear, state
    aggregate: TotalRedemtionChecks
    order_by:  BackgroundCheckYear desc, TotalRedemtionChecks desc
    limit: 500000
    nest: types_of_guns is {
        group_by:  redemption_handgun,redemption_long_gun, redemption_other
        limit: 500000
    }
}

# line_chart
run: background_check -> {
    group_by:  BackgroundCheckYear
    aggregate: RedemptionHandGun, RedemptionLongGun,RedemptionOther
}

# shape_map
run: background_check -> {
    group_by: state
    aggregate: TotalRedemtionChecks
}
>>>markdown
#### RedemPre-Pawn Background Checks
Pre-Pawn background checks requested by an officially-licensed FFL on prospective firearm transferees seeking to pledge or pawn a firearm as security for the payment or repayment of money, prior to actually pledging or pawning the firearm.
>>>malloy
run: background_check -> {
    aggregate: PrePawnTotal, PrePawnRate

    nest: types_of_guns is {
        aggregate: PrePawnHandgun, PrePawnLongGun,PrePawnOther
    }
}

run: background_check -> {
    group_by:  BackgroundCheckYear, state
    aggregate: PrePawnTotal
    order_by:  BackgroundCheckYear desc, PrePawnTotal desc
    limit: 500000
    nest: types_of_guns is {
        group_by:  prepawn_handgun,prepawn_long_gun, prepawn_other
        limit: 500000
    }
}

# line_chart
run: background_check -> {
    group_by:  BackgroundCheckYear
    aggregate: PrePawnHandgun,PrePawnLongGun, PrePawnOther
}

# shape_map
run: background_check -> {
    group_by: state
    aggregate: PrePawnTotal
}
>>>markdown
#### Returned Firearms
>>>malloy
run: background_check -> {
    aggregate: TotalReturnedChecks, ReturnedRate

    nest: types_of_guns is {
        aggregate: ReturnedHandgun, ReturnedLongGun,ReturnedOther
    }
}

run: background_check -> {
    group_by:  BackgroundCheckYear, state
    aggregate: TotalReturnedChecks
    order_by:  BackgroundCheckYear desc, TotalReturnedChecks desc
    limit: 500000
    nest: types_of_guns is {
        group_by:  returned_handgun,returned_long_gun, returned_other
        limit: 500000
    }
}

# line_chart
run: background_check -> {
    group_by:  BackgroundCheckYear
    aggregate: ReturnedHandgun, ReturnedLongGun,ReturnedOther
}

# shape_map
run: background_check -> {
    group_by: state
    aggregate: TotalReturnedChecks
}
>>>markdown
#### Private Firearms Sales
>>>malloy
run: background_check -> {
    aggregate: TotalPrivateSaleChecks, PrivateSaleRate

    nest: types_of_guns is {
        aggregate: PrivateSaleHandgun, PrivateSaleLongGun,PrivateSaleOther
    }
}

run: background_check -> {
    group_by:  BackgroundCheckYear, state
    aggregate: TotalPrivateSaleChecks
    order_by:  BackgroundCheckYear desc, TotalPrivateSaleChecks desc
    limit: 500000
    nest: types_of_guns is {
        group_by:  private_sale_handgun,private_sale_long_gun, private_sale_other
        limit: 500000
    }
}

# line_chart
run: background_check -> {
    group_by:  BackgroundCheckYear
    aggregate: PrivateSaleHandgun, PrivateSaleLongGun,PrivateSaleOther
}

# shape_map
run: background_check -> {
    group_by: state
    aggregate: TotalPrivateSaleChecks
}
>>>markdown
#### Firearms returned to seller
>>>malloy
run: background_check -> {
    aggregate: TotalReturntoSellerChecks,ReturntoSellerRate

    nest: types_of_guns is {
        aggregate: ReturntoSellerHandgun, ReturntoSellerLongGun, ReturntoSellerOther
    }
}

run: background_check -> {
    group_by:  BackgroundCheckYear, state
    aggregate: TotalReturntoSellerChecks
    order_by:  BackgroundCheckYear desc, TotalReturntoSellerChecks desc
    limit: 500000
    nest: types_of_guns is {
        group_by:  return_to_seller_handgun,return_to_seller_long_gun, return_to_seller_other
        limit: 500000
    }
}

# line_chart
run: background_check -> {
    group_by:  BackgroundCheckYear
    aggregate: ReturntoSellerHandgun, ReturntoSellerLongGun, ReturntoSellerOther
}

# shape_map
run: background_check -> {
    group_by: state
    aggregate: TotalReturntoSellerChecks
}
>>>markdown
Concealed carry laws vary by state. Some states allow people to carry concealed handguns without a permit, while others require a permit.
>>>malloy
run: background_check -> {
    aggregate: PermitChecks, PermitRechecks, PermitCheckRate

    nest: types_of_guns is {
        group_by: state
        aggregate: PermitChecks, PermitRechecks, PermitCheckRate
    }
}
>>>markdown
Deaths in the US by firearms in 2022.
>>>malloy
run: firearm_deaths -> {
    aggregate: total_deaths
    where: death_year = "2022"
}

run: firearm_deaths -> {
    group_by: states_lookup_by_code.state
    aggregate: total_deaths
    where: death_year = "2022"
}

# shape_map
run: firearm_deaths -> {
    group_by: states_lookup_by_code.state
    aggregate: total_deaths
    where: death_year = "2022"
}
>>>markdown
Total Deaths related from firearms from to 2022
>>>malloy
run: firearm_deaths -> {
    aggregate: total_deaths
}

run: firearm_deaths -> {
    group_by: death_year
    aggregate: total_deaths
    order_by: death_year desc
}

# line_chart
run: firearm_deaths -> {
    group_by: death_year
    aggregate: total_deaths
    order_by: death_year
}

# shape_map
run: firearm_deaths -> {
    group_by: states_lookup_by_code.state
    aggregate: total_deaths
    where: death_year = "2022"
}